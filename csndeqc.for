C
C**********************************************************************C
C**********************************************************************C
C**********************************************************************C
C
      REAL FUNCTION CSNDEQC(SNDDIA,SSG,WS,TAUR,TAUB,D50,SIGPHI,
     &                SNDDMX,VDR,IOPT,ISNDAL)
C
C **  THIS SUBROUTINE IS PART OF  EFDC-FULL VERSION 1.0a 
C
C **  LAST MODIFIED BY JOHN HAMRICK ON 1 NOVEMBER 2001
C
C----------------------------------------------------------------------C
C
C CHANGE RECORD
C DATE MODIFIED     BY                 DATE APPROVED    BY
C
C----------------------------------------------------------------------C
C
      INCLUDE 'EFDC.PAR'
C
C **  CALCULATES NEAR BED REFERENCE CONCENTRATION FOR NONCOHESIVE 
C **  SEDIMENT 
C
C     SNDDIA = SAND GRAIN DIAMETER 
C     SSG    = SAND GRAIN SPECIFIC GRAVITY 
C     WS     = SAND GRAIN SETTLING VELOCITY
C     TAUR   = WATER DENSITY NORMALIZED CRITICAL SHIELDS STRESS
C     TAUB   = WATER DENSITY NORMALIZED BED STRESS
C     SSG    = SAND SPECIFIC GRAVITY 
C     SIGPHI = PHI SIZE STANDARD DEVIATION
C     SNDDMX = D90 SEDIMENT DIAMETER OR MAX SEDIMENT DIAMETER
C
C **  IOPT=1  BASED ON 
C **
C **  GARCIA, M., AND G. PARKER, 1991: ENTRAINMENT OF BED SEDIMENT
C **  INTO SUSPENSION, J. HYDRAULIC ENGINEERING, 117, 414-435.
C
      IF(IOPT.EQ.1)THEN
        REY=1.E6*SNDDIA*SQRT( 9.8*(SSG-1.)*SNDDIA )   !EQ 42
        REY=REY**0.6                                  !SEE EQ 43
        DFAC=1.
        IF(ISNDAL.GE.1) DFAC=(SNDDIA/D50)**0.2        !SEE EQ 43
        RLAM=1.-0.29*SIGPHI                           !EQ 51
        USTAR=SQRT(TAUB)
        VAL=DFAC*RLAM*REY*USTAR/WS                    !Z IN EQ 43
        VAL=1.3E-7*(VAL**5)                           !TOP OF EQ 45
        TMP=VAL/(1+3.33*VAL)                          !EQ 45
        CSNDEQC=1.E6*SSG*TMP                          !CONVERT TO MASS CONC
CJH **  IF USTAR < WS SET EQUILBRIUM NEAR BED CONCENTRATION TO ZERO
CJH **  TRANSPORT VIA BED LOAD IMPLEMENTED IN SSEDTOX FOR 
CJH **  USTAR**2 > CRITICAL SHIELDS AND USTAR < WS
        IF(USTAR.LT.WS) CSNDEQC=0.
C       WRITE(6,600)CSNDEQC,SNDDIA,SSG,WS,TAUR,TAUB,D50,SIGPHI,
C     &                      SNDDMX
       ENDIF
C
C **  IOPT=2  BASED ON 
C **
C **  SMITH, J. D., AND S. R. MCLEAN, 1977: SPATIALLY AVERAGED FLOW
C **  OVER A WAVY SURFACE, J. GEOPHYSICAL RESEARCH, 82, 1735-1746.
C
      IF(IOPT.EQ.2)THEN
        VAL=2.4E-3*( (TAUB/TAUR)-1. )
        VAL=MAX(VAL,0.)
        TMP=0.65*VAL/(1.+VAL)
        CSNDEQC=1.E6*SSG*TMP
CJH **  IF USTAR < WS SET EQUILBRIUM NEAR BED CONCENTRATION TO ZERO
CJH **  TRANSPORT VIA BED LOAD IMPLEMENTED IN SSEDTOX FOR 
CJH **  USTAR**2 > CRITICAL SHIELDS AND USTAR < WS
        USTAR=SQRT(TAUB)
        IF(USTAR.LT.WS) CSNDEQC=0.
C       WRITE(6,600)CSNDEQC,SNDDIA,SSG,WS,TAUR,TAUB,D50,SIGPHI,
C     &                      SNDDMX
       ENDIF
C
C **  IOPT=3  BASED ON 
C **
C **  VAN RIJN, L. C., 1984: SEDIMENT TRANSPORT, PART II: SUSPENDED
C **  LOAD TRANSPORT, J. HYDRAULIC ENGINEERING, 110, 1623-1641.
C
      IF(IOPT.EQ.3)THEN
        REY=1.E4*SNDDIA*( (9.8*(SSG-1.))**0.333 )
        IF(REY.LE.10.) TAURS=(4.*WS/REY)**2
        IF(REY.GT.10.) TAURS=0.016*WS*WS
        REY3=REY**0.3
        VAL=(TAUB/TAURS)-1.
C        VAL=(TAUB/TAUR)-1.
        VAL=MAX(VAL,0.)
        VAL=VAL**1.5
        RATIO=SNDDIA/(3.*SNDDMX)
        TMP=0.015*RATIO*VAL/REY3
        CSNDEQC=1.E6*SSG*TMP
CJH **  IF USTAR < WS SET EQUILBRIUM NEAR BED CONCENTRATION TO ZERO
CJH **  TRANSPORT VIA BED LOAD IMPLEMENTED IN SSEDTOX FOR 
CJH **  USTAR**2 > CRITICAL SHIELDS AND USTAR < WS
        USTAR=SQRT(TAUB)
        IF(USTAR.LT.WS) CSNDEQC=0.
C       WRITE(6,600)CSNDEQC,SNDDIA,SSG,WS,TAUR,TAUB,D50,SIGPHI,
C     &                      SNDDMX
       ENDIF
C
C **  IOPT=4  BASED ON 
C **
C **  J.M. HAMRICK'S PARAMETERIZATION OF SEDFLUME DATA NO CRITICAL STRESS
C
      IF(IOPT.EQ.4)THEN
        REY=1.E4*SNDDIA*( (9.8*(SSG-1.))**0.333 )
	  TMPVAL=SQRT(TAUB)/WS
        REY3=REY**1.333
        TMPVAL=REY3*TMPVAL
        VAL=(TMPVAL-1.0)**5.
	  VAL=4.E-9*VAL
        CSNDEQC=1.E6*SSG*VAL/(1.+VDR)
CJH **  IF USTAR < WS SET EQUILBRIUM NEAR BED CONCENTRATION TO ZERO
CJH **  TRANSPORT VIA BED LOAD IMPLEMENTED IN SSEDTOX FOR 
CJH **  USTAR**2 > CRITICAL SHIELDS AND USTAR < WS
        USTAR=SQRT(TAUB)
        IF(USTAR.LT.WS) CSNDEQC=0.
      ENDIF
C
C **  IOPT=5  BASED ON 
C **
C **  J.M. HAMRICK'S PARAMETERIZATION OF SEDFLUME DATA WITH CRITICAL STRESS
C
      IF(IOPT.EQ.5)THEN
        REY=1.E4*SNDDIA*( (9.8*(SSG-1.))**0.333 )
	  TMPVAL=SQRT(TAUB)/WS
        REY3=REY**1.333
        TMPVAL=REY3*TMPVAL
	  VAL=0.0
	  IF(TMPVAL.GT.1.0) VAL=(TMPVAL-1.0)**5.
	  VAL=4.E-9*VAL
        CSNDEQC=1.E6*SSG*VAL/(1.+VDR)
CJH **  IF USTAR < WS SET EQUILBRIUM NEAR BED CONCENTRATION TO ZERO
CJH **  TRANSPORT VIA BED LOAD IMPLEMENTED IN SSEDTOX FOR 
CJH **  USTAR**2 > CRITICAL SHIELDS AND USTAR < WS
        USTAR=SQRT(TAUB)
        IF(USTAR.LT.WS) CSNDEQC=0.
      ENDIF

  600 FORMAT(10E12.4)
      RETURN
      END 
