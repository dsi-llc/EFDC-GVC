C
C**********************************************************************C
C**********************************************************************C
C**********************************************************************C
C
      REAL FUNCTION CSNDZEQ(SNDDIA,GPDIASED,TAUR,TAUB,SNDDMX,DEP,
     &    IOPT,SSG,WS)  
C
C **  THIS SUBROUTINE IS PART OF  EFDC-FULL VERSION 1.0a 
C
C **  LAST MODIFIED BY JOHN HAMRICK ON 1 NOVEMBER 2001
C
C----------------------------------------------------------------------C
C
C CHANGE RECORD
C DATE MODIFIED     BY                 DATE APPROVED    BY
C
C----------------------------------------------------------------------C
C
      INCLUDE 'EFDC.PAR'
C
C **  CALCULATES NEAR BED REFERENCE CONCENTRATION REFERENCE HEIGHT
C
C     SNDDIA = SAND GRAIN DIAMETER 
C     SSG    = SAND GRAIN SPECIFIC GRAVITY 
C     WS     = SAND GRAIN SETTLING VELOCITY
C     TAUR   = WATER DENSITY NORMALIZED CRITICAL SHIELDS STRESS
C     TAUB   = WATER DENSITY NORMALIZED BED STRESS
C     SSG    = SAND SPECIFIC GRAVITY 
C     SIGPHI = PHI SIZE STANDARD DEVIATION
C     SNDDMX = D90 SEDIMENT DIAMETER OR MAX SEDIMENT DIAMETER
C
C **  IOPT=1  BASED ON 
C **
C **  GARCIA, M., AND G. PARKER, 1991: ENTRAINMENT OF BED SEDIMENT
C **  INTO SUSPENSION, J. HYDRAULIC ENGINEERING, 117, 414-435.
C
      IF(IOPT.EQ.1)THEN
        CSNDZEQ=0.05
      ENDIF
C
C **  IOPT=2  BASED ON 
C **
C **  SMITH, J. D., AND S. R. MCLEAN, 1977: SPATIALLY AVERAGED FLOW
C **  OVER A WAVY SURFACE, J. GEOPHYSICAL RESEARCH, 82, 1735-1746.
C
      IF(IOPT.EQ.2)THEN
        TMPVAL=26.3*SNDDMX*(TAUB-TAUR)/GPDIASED
        TMPVAL=TMPVAL*SNDDIA/SNDDMX
        TMPVAL=TMPVAL/DEP
        CSNDZEQ=MAX(TMPVAL,0.01)
      ENDIF
C
C **  IOPT=3  BASED ON 
C **
C **  VAN RIJN, L. C., 1984: SEDIMENT TRANSPORT, PART II: SUSPENDED
C **  LOAD TRANSPORT, J. HYDRAULIC ENGINEERING, 110, 1623-1641.
C
      IF(IOPT.EQ.3)THEN
        REY=1.E4*SNDDIA*( (9.8*(SSG-1.))**0.333 )
        IF(REY.LE.10.) TAURS=(4.*WS/REY)**2
        IF(REY.GT.10.) TAURS=0.016*WS*WS
        VAL=(TAUB/TAURS)-1.
C        VAL=(TAUB/TAUR)-1.
        VAL=MAX(VAL,0.)
        VAL1=1.-EXP(-0.5*VAL)
        VAL1=0.11*VAL1*(25.-VAL)
        ZEQ1=0.5*VAL1*(DEP**0.7)*(SNDDMX**0.3)
        ZEQ1=ZEQ1/DEP
        CSNDZEQ=MAX(ZEQ1,0.01)
      ENDIF
C
      RETURN
      END 
